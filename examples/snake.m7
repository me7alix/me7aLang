import "std.m7"
import "raylib.m7"

def TILE_SIZE  30
def MAP_WIDTH  20
def MAP_HEIGHT 20
def MAP_SIZE   (MAP_WIDTH * MAP_HEIGHT)

struct Vec2 {
	x: int,
	y: int,
}

snake:     [MAP_SIZE]Vec2
snake_dir: Vec2
snake_len: uptr
apple:     Vec2

fn draw_game() {
	for i: uptr = 0; i < snake_len; i+=1 {
		rl_draw_rect(snake[i].x * TILE_SIZE, snake[i].y * TILE_SIZE, TILE_SIZE, TILE_SIZE, rl_ColorGreen)
	}

	rl_draw_rect(apple.x * TILE_SIZE, apple.y * TILE_SIZE, TILE_SIZE, TILE_SIZE, rl_ColorRed)

	score_text: [64]i8
	sprintf(score_text:*i8, "Score: %zu", snake_len - 2)
	rl_draw_text(score_text:*i8, 20, 20, 20, rl_ColorWhite)
}

fn snake_follow() {
	for i := snake_len - 1; i >= 1; i-=1 {
		snake[i].x = snake[i - 1].x
		snake[i].y = snake[i - 1].y
	}
}

fn apple_set_rand_pos() {
	apple.x = rand() % MAP_WIDTH
	apple.y = rand() % MAP_HEIGHT
}

fn snake_eat_apple() {
	if snake[0].x == apple.x && snake[0].y == apple.y {
		apple_set_rand_pos()
		snake[snake_len].x = snake[snake_len - 1].x
		snake[snake_len].y = snake[snake_len - 1].y
		snake_len += 1
	}
}

fn is_game_over(): bool {
	for i := snake_len - 1; i >= 1; i-=1 {
		if snake[0].x == snake[i].x && snake[0].y == snake[i].y {
			return true
		}
	}

	return false
}

fn snake_set_dir() {
	if rl_is_key_pressed(rl_KeyS) && snake_dir.y == 0 {
		snake_dir.x = 0
		snake_dir.y = 1
	}

	if rl_is_key_pressed(rl_KeyW) && snake_dir.y == 0 {
		snake_dir.x = 0
		snake_dir.y = -1
	}

	if rl_is_key_pressed(rl_KeyD) && snake_dir.x == 0 {
		snake_dir.x = 1
		snake_dir.y = 0
	}

	if rl_is_key_pressed(rl_KeyA) && snake_dir.x == 0 {
		snake_dir.x = -1
		snake_dir.y = 0
	}
}

fn snake_movement() {
	snake_follow()
	snake[0].x += snake_dir.x
	snake[0].y += snake_dir.y

	if snake[0].x == MAP_WIDTH {
		snake[0].x = 0
	} else if snake[0].x == -1 {
		snake[0].x = MAP_WIDTH - 1
	} else if snake[0].y == MAP_HEIGHT {
		snake[0].y = 0
	} else if snake[0].y == -1 {
		snake[0].y = MAP_HEIGHT - 1
	}
}

fn game_init() {
	apple_set_rand_pos()
	snake_len = 2
	snake[0].x = 1
	snake[0].y = 0
	snake[1].x = 0
	snake[1].y = 0
	snake_dir.x = 1
	snake_dir.y = 0
}

fn main() {
	srand(time(null))
	game_init()

	rl_init_window(MAP_WIDTH * TILE_SIZE, MAP_HEIGHT * TILE_SIZE, "Snake")
	rl_set_target_fps(60)

	timer    := 0
	new_game := false

	while !rl_window_should_close() {
		rl_begin_drawing()
		rl_clear_bg(rl_ColorBlack)

		if new_game {
			draw_game()

			rl_draw_text("Game over!", 200, MAP_HEIGHT/2 * TILE_SIZE - 20, 40, rl_ColorWhite)
			rl_draw_text("Press SPACE to restart", 200, MAP_HEIGHT/2 * TILE_SIZE + 20, 18, rl_ColorWhite)

			if rl_is_key_pressed(rl_KeySpace) {
				game_init()
				new_game = false
			}
		} else {
			snake_set_dir()

			if timer % 12 == 0 {
				snake_movement()

				if is_game_over() {
					new_game = true
					continue
				}

				snake_eat_apple()
				timer = 0
			}

			draw_game()
		}

		timer += 1
		rl_end_drawing()
	}

	rl_close_window()
}
