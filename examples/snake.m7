import "std.m7"
import "raylib.m7"

#MAP_W   20
#MAP_H   20
#TILE    30
#MAX_LEN 1024

apple_x: int
apple_y: int

snake_x:   [MAX_LEN]int
snake_y:   [MAX_LEN]int
snake_dx:  int
snake_dy:  int
snake_da:  bool
snake_len: uptr

fn draw_game() {
	for i: uptr = 0; i < snake_len; i+=1 {
		rl_draw_rect(snake_x[i] * TILE, snake_y[i] * TILE, TILE, TILE, rl_ColorGreen)
	}

	rl_draw_rect(apple_x * TILE, apple_y * TILE, TILE, TILE, rl_ColorRed)

	score_buf: [64]i8
	sprintf(score_buf:*i8, "Score: %zu", snake_len - 2)
	rl_draw_text(score_buf:*i8, 20, 20, 20, rl_ColorWhite)
}

fn snake_follow() {
	for i: uptr = snake_len; i >= 1; i-=1 {
		snake_x[i] = snake_x[i - 1]
		snake_y[i] = snake_y[i - 1]
	}
}

fn apple_set_rand_pos() {
	apple_x = rand() % MAP_W
	apple_y = rand() % MAP_H
}

fn snake_eat_apple() {
	if snake_x[0] == apple_x && snake_y[0] == apple_y {
		apple_set_rand_pos()
		snake_x[snake_len] = snake_x[snake_len - 1]
		snake_y[snake_len] = snake_y[snake_len - 1]
		snake_len += 1
	}
}

fn is_game_over(): bool {
	for i: uptr = snake_len; i >= 1; i-=1 {
		if snake_x[0] == snake_x[i] && snake_y[0] == snake_y[i] {
			return true
		}
	}

	return false
}

fn snake_set_dir() {
	if !snake_da {
		return
	}

	if rl_is_key_pressed(rl_KeyS) && snake_dy == 0 {
		snake_dx = 0
		snake_dy = 1
		snake_da = false
	}

	if rl_is_key_pressed(rl_KeyW) && snake_dy == 0 {
		snake_dx = 0
		snake_dy = -1
		snake_da = false
	}

	if rl_is_key_pressed(rl_KeyD) && snake_dx == 0 {
		snake_dx = 1
		snake_dy = 0
		snake_da = false
	}

	if rl_is_key_pressed(rl_KeyA) && snake_dx == 0 {
		snake_dx = -1
		snake_dy = 0
		snake_da = false
	}
}

fn snake_movement() {
	snake_follow()
	snake_x[0] += snake_dx
	snake_y[0] += snake_dy

	if snake_x[0] == MAP_W {
		snake_x[0] = 0
	} else if snake_x[0] == -1 {
		snake_x[0] = MAP_W - 1
	} else if snake_y[0] == MAP_H {
		snake_y[0] = 0
	} else if snake_y[0] == -1 {
		snake_y[0] = MAP_H - 1
	}
}

fn game_init() {
	apple_set_rand_pos()
	snake_len = 2
	snake_x[0] = 1
	snake_y[0] = 0
	snake_x[1] = 0
	snake_y[1] = 0
	snake_dx = 1
	snake_dy = 0
}

fn main() {
	srand(time(null))
	game_init()

	rl_init_window(MAP_W * TILE, MAP_H * TILE, "Snake")
	rl_set_target_fps(60)

	timer := 0
	new_game := false
	snake_da = true
	while !rl_window_should_close() {
		rl_begin_drawing()
		rl_clear_bg(rl_ColorBlack)

		if new_game {
			rl_draw_text("Game over!", 200, MAP_H/2 * TILE - 20, 40, rl_ColorWhite)
			rl_draw_text("Press SPACE to restart", 200, MAP_H/2 * TILE + 20, 18, rl_ColorWhite)

			if rl_is_key_pressed(rl_KeySpace) {
				game_init()
				new_game = false
			}
		} else {
			if timer % 12 == 0 {
				snake_da = true
			}

			snake_set_dir()

			if timer % 12 == 0 {
				snake_movement()

				if is_game_over() {
					new_game = true
					continue
				}

				snake_eat_apple()
				timer = 0
			}
		}

		draw_game()

		timer += 1
		rl_end_drawing()
	}

	rl_close_window()
}
